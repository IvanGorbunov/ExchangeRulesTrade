Запрос = Новый Запрос("ВЫБРАТЬ
|	ЦеныНоменклатуры.ТипЦен КАК ВидЦены,
|	ЦеныНоменклатуры.Номенклатура КАК Номенклатура,
|	ЦеныНоменклатуры.ХарактеристикаНоменклатуры КАК Характеристика,
|	ВЫРАЗИТЬ(ЦеныНоменклатуры.Цена * ВЫБОР
|			КОГДА ЦеныНоменклатуры.ТипЦен.ВалютаЦены <> ЦеныНоменклатуры.Валюта
|				ТОГДА ВЫБОР
|						КОГДА ЕСТЬNULL(КурсыСрезПоследнихВалютаЦены.Кратность, 0) > 0
|								И ЕСТЬNULL(КурсыСрезПоследнихВалютаЦены.Курс, 0) > 0
|								И ЕСТЬNULL(КурсыСрезПоследнихВалютаДокумента.Кратность, 0) > 0
|								И ЕСТЬNULL(КурсыСрезПоследнихВалютаДокумента.Курс, 0) > 0
|							ТОГДА КурсыСрезПоследнихВалютаЦены.Курс * КурсыСрезПоследнихВалютаДокумента.Кратность / (КурсыСрезПоследнихВалютаДокумента.Курс * КурсыСрезПоследнихВалютаЦены.Кратность)
|						ИНАЧЕ 0
|					КОНЕЦ
|			ИНАЧЕ 1
|		КОНЕЦ КАК ЧИСЛО(10, 2)) КАК Цена,
|	ЦеныНоменклатуры.ЕдиницаИзмерения КАК Упаковка
|ИЗ
|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&ДатаОстатков, ) КАК ЦеныНоменклатуры
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, ) КАК КурсыСрезПоследнихВалютаЦены
|		ПО (КурсыСрезПоследнихВалютаЦены.Валюта = ЦеныНоменклатуры.Валюта)
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, ) КАК КурсыСрезПоследнихВалютаДокумента
|		ПО (КурсыСрезПоследнихВалютаДокумента.Валюта = ЦеныНоменклатуры.ТипЦен.ВалютаЦены)
|ИТОГИ ПО
|	ВидЦены");

Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(Параметры.ДатаОстатков));

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("ВидЦены");
ВыборкаДанных.Колонки.Добавить("Товары");

ВыборкаПоВидамЦен = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоВидамЦен.Следующий() Цикл
	
	НоваяСтрока = ВыборкаДанных.Добавить();
	НоваяСтрока.Дата = КонецДня(Параметры.ДатаОстатков);
	НоваяСтрока.ВидЦены = ВыборкаПоВидамЦен.ВидЦены;
	
	НоваяСтрока.Товары = Новый ТаблицаЗначений;
	НоваяСтрока.Товары.Колонки.Добавить("Номенклатура");
	НоваяСтрока.Товары.Колонки.Добавить("Характеристика");
	НоваяСтрока.Товары.Колонки.Добавить("Упаковка");
	НоваяСтрока.Товары.Колонки.Добавить("Цена");
	НоваяСтрока.Товары.Колонки.Добавить("ВидЦены");
	
	ДетальнаяВыборка = ВыборкаПоВидамЦен.Выбрать();
	Пока ДетальнаяВыборка.Следующий() Цикл
		НоваяСтрокаТовары = НоваяСтрока.Товары.Добавить();	
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, ДетальнаяВыборка);
	КонецЦикла;
КонецЦикла;
