Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка          КАК ДоговорКонтрагента,
	|	ДокТовары.Ссылка                     КАК Ссылка,
	|	ДокТовары.Ссылка.Номер               КАК Номер,
	|	ДокТовары.Ссылка.Дата                КАК Дата,
	|	ДокТовары.Ссылка.Проведен            КАК Проведен,
	|	ДокТовары.Ссылка.ПометкаУдаления     КАК ПометкаУдаления,
	|	ДокТовары.Ссылка.Комментарий         КАК Комментарий,
	|	ДокТовары.Ссылка.Контрагент          КАК Контрагент,
	|	ДокТовары.Ссылка.Ответственный       КАК Ответственный,
	|	ДокТовары.ТипЦен                     КАК ТипЦен,
	|	ДокТовары.Номенклатура               КАК Номенклатура,
	|	ДокТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	|	
	|	ДокТовары.Цена * ВЫБОР
	|		КОГДА ДоговорыКонтрагентов.ВалютаВзаиморасчетов <> ДокТовары.Валюта
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(КурсыВалютыЦены.Кратность, 0) > 0
	|							И ЕСТЬNULL(КурсыВалютыЦены.Курс, 0) > 0
	|							И ЕСТЬNULL(КурсыВалютыТипаЦен.Кратность, 0) > 0
	|							И ЕСТЬNULL(КурсыВалютыТипаЦен.Курс, 0) > 0
	|						ТОГДА КурсыВалютыЦены.Курс * КурсыВалютыТипаЦен.Кратность
	|							/ (КурсыВалютыТипаЦен.Курс * КурсыВалютыЦены.Кратность)
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ 1
	|	КОНЕЦ                      КАК Цена,
	|	
	|	ДокТовары.ЕдиницаИзмерения КАК ЕдиницаИзмерения
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УстановкаЦенНоменклатурыКонтрагентов.Товары КАК ДокТовары
	|		ПО (ДокТовары.ТипЦен = ДоговорыКонтрагентов.ТипЦен)
	|		
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, ) КАК КурсыВалютыЦены
	|			ПО (ДокТовары.Валюта = КурсыВалютыЦены.Валюта)
	|			
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, ) КАК КурсыВалютыТипаЦен
	|			ПО (ДоговорыКонтрагентов.ВалютаВзаиморасчетов = КурсыВалютыТипаЦен.Валюта)
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком)
	|ИТОГИ ПО
	|	ДоговорКонтрагента,
	|	Ссылка,
	|	ТипЦен");

Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(Параметры.ДатаОстатков));

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("Проведен");
ВыборкаДанных.Колонки.Добавить("ПометкаУдаления");
ВыборкаДанных.Колонки.Добавить("Ответственный");
ВыборкаДанных.Колонки.Добавить("Комментарий");

ВыборкаДанных.Колонки.Добавить("Партнер");
ВыборкаДанных.Колонки.Добавить("Соглашение");

ВыборкаДанных.Колонки.Добавить("Товары");


РезультатыЗапроса = Запрос.Выполнить();
ВыборкаПоДоговорамКонтрагента = РезультатыЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоДоговорамКонтрагента.Следующий() Цикл
	
	ВыборкаПоУстановкамЦенНоменклатуры = ВыборкаПоДоговорамКонтрагента.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоУстановкамЦенНоменклатуры.Следующий() Цикл
		
		ВыборкаПоТипамЦен = ВыборкаПоУстановкамЦенНоменклатуры.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоТипамЦен.Следующий() Цикл
			
			НоваяСтрока = ВыборкаДанных.Добавить();
			НоваяСтрока.Дата            = НачалоДня(ВыборкаПоУстановкамЦенНоменклатуры.Дата); 
			НоваяСтрока.Проведен        = ВыборкаПоУстановкамЦенНоменклатуры.Проведен; 
			НоваяСтрока.ПометкаУдаления = ВыборкаПоУстановкамЦенНоменклатуры.ПометкаУдаления; 
			НоваяСтрока.Ответственный   = ВыборкаПоУстановкамЦенНоменклатуры.Ответственный; 
			НоваяСтрока.Комментарий     = ВыборкаПоУстановкамЦенНоменклатуры.Комментарий; 
			
			НоваяСтрока.Партнер    = ВыборкаПоУстановкамЦенНоменклатуры.Контрагент; 
			НоваяСтрока.Соглашение = ВыборкаПоДоговорамКонтрагента.ДоговорКонтрагента; 
			
			НоваяСтрока.Товары = Новый ТаблицаЗначений;
			НоваяСтрока.Товары.Колонки.Добавить("Номенклатура");
			НоваяСтрока.Товары.Колонки.Добавить("Характеристика");
			НоваяСтрока.Товары.Колонки.Добавить("Упаковка");
			НоваяСтрока.Товары.Колонки.Добавить("ВидЦеныПоставщика");
			НоваяСтрока.Товары.Колонки.Добавить("Цена");
			
			
			ВыборкаПоДетальнымЗаписям = ВыборкаПоТипамЦен.Выбрать();
			Пока ВыборкаПоДетальнымЗаписям.Следующий() Цикл
				
				НоваяСтрокаТовары = НоваяСтрока.Товары.Добавить();
				НоваяСтрокаТовары.Номенклатура   = ВыборкаПоДетальнымЗаписям.Номенклатура; 
				НоваяСтрокаТовары.Характеристика = ВыборкаПоДетальнымЗаписям.ХарактеристикаНоменклатуры; 
				НоваяСтрокаТовары.Упаковка       = ВыборкаПоДетальнымЗаписям.ЕдиницаИзмерения; 
				
				НоваяСтрокаТовары.ВидЦеныПоставщика = Новый Структура;
				НоваяСтрокаТовары.ВидЦеныПоставщика.Вставить("Владелец",        НоваяСтрока.Партнер);
				НоваяСтрокаТовары.ВидЦеныПоставщика.Вставить("ЦенаВключаетНДС", ВыборкаПоДетальнымЗаписям.ТипЦен.ЦенаВключаетНДС);
				НоваяСтрокаТовары.ВидЦеныПоставщика.Вставить("Наименование",    ВыборкаПоДетальнымЗаписям.ТипЦен.Наименование);
				НоваяСтрокаТовары.ВидЦеныПоставщика.Вставить("ПометкаУдаления", ВыборкаПоДетальнымЗаписям.ТипЦен.ПометкаУдаления);
				
				НоваяСтрокаТовары.Цена = ВыборкаПоДетальнымЗаписям.Цена; 
				
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
КонецЦикла;
