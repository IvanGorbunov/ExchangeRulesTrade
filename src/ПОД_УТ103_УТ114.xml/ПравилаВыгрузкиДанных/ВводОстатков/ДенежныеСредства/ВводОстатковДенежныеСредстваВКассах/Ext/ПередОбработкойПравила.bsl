Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ОстаткиВКассах.Организация                                                         КАК Организация,
	|	ОстаткиВКассах.БанковскийСчетКасса                                                 КАК Касса,
	|	ОстаткиВКассах.СуммаОстаток                                                        КАК Сумма,
	|	ОстаткиВКассах.СуммаОстаток * ВЫБОР
	|		КОГДА ЕСТЬNULL(КурсыВалют.Курс, 0) > 0
	|				И ЕСТЬNULL(КурсыВалют.Кратность, 0) > 0
	|			ТОГДА КурсыВалют.Курс / КурсыВалют.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                                              КАК СуммаРегл,
	|	ОстаткиВКассах.СуммаУпрОстаток													   КАК СуммаУпр
	|ИЗ
	|	РегистрНакопления.ДенежныеСредства.Остатки(
	|		&ДатаОстатков,
	|		ВидДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ВидыДенежныхСредств.Наличные)) КАК ОстаткиВКассах
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков) КАК КурсыВалют
	|		ПО ОстаткиВКассах.БанковскийСчетКасса.ВалютаДенежныхСредств = КурсыВалют.Валюта
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ОстаткиВКассах.БанковскийСчетКасса
	|ИТОГИ ПО
	|	Организация
	|АВТОУПОРЯДОЧИВАНИЕ");
	
Если Параметры.Организации.Количество() > 0 Тогда
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВидДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ВидыДенежныхСредств.Наличные)", 
								"ВидДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ВидыДенежныхСредств.Наличные) И Организация В (&МассивОрганизаций)");
	Запрос.УстановитьПараметр("МассивОрганизаций", Параметры.Организации);
КонецЕсли;	

Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(Параметры.ДатаОстатков));

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("Организация");
ВыборкаДанных.Колонки.Добавить("Кассы");

РезультатыЗапроса = Запрос.Выполнить();
ВыборкаПоОрганизациям = РезультатыЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоОрганизациям.Следующий() Цикл
	
	НоваяСтрока = ВыборкаДанных.Добавить();
	НоваяСтрока.Дата        = КонецДня(Параметры.ДатаОстатков); 
	НоваяСтрока.Организация = ВыборкаПоОрганизациям.Организация; 
	
	НоваяСтрока.Кассы = Новый ТаблицаЗначений;
	НоваяСтрока.Кассы.Колонки.Добавить("Касса");
	НоваяСтрока.Кассы.Колонки.Добавить("Сумма");
	НоваяСтрока.Кассы.Колонки.Добавить("СуммаРегл");
	НоваяСтрока.Кассы.Колонки.Добавить("СуммаУпр");
	
	ВыборкаДетальныеЗаписи = ВыборкаПоОрганизациям.Выбрать(); 
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если ВыборкаДетальныеЗаписи.Сумма < 0 Тогда
			СтрокаСообщения = НСтр("ru = 'Обнаружены отрицательные остатки по кассе ""%1"" при выгрузке по правилу: ""%2""'");
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1", ВыборкаДетальныеЗаписи.Касса);
			СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2", НСтр("ru = 'Денежные средства в кассах'"));
			ЗаписатьВПротоколВыполнения(СтрокаСообщения,,Ложь);
			Продолжить;
		КонецЕсли;
		
		ВалютаДенежныхСредств = ОбщегоНазначения.ПолучитьЗначениеРеквизита(ВыборкаДетальныеЗаписи.Касса, "ВалютаДенежныхСредств");		
		
		НоваяСтрокаКассы = НоваяСтрока.Кассы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаКассы, ВыборкаДетальныеЗаписи);
			
	КонецЦикла;
	
	Если НоваяСтрока.Кассы.Количество() = 0 Тогда
		ВыборкаДанных.Удалить(НоваяСтрока);
	КонецЕсли;
	
КонецЦикла;
