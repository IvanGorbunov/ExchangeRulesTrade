Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВзаиморасчетыСКонтрагентамиОстатки.Сделка                              КАК Сделка,
	|	ВзаиморасчетыСКонтрагентамиОстатки.Организация                         КАК Организация,
	|	ВзаиморасчетыСКонтрагентамиОстатки.Контрагент                          КАК Контрагент,
	|	ВзаиморасчетыСКонтрагентамиОстатки.Контрагент                          КАК Партнер,
	|	-ВзаиморасчетыСКонтрагентамиОстатки.СуммаВзаиморасчетовОстаток         КАК Сумма,
	|	
	|	-ВзаиморасчетыСКонтрагентамиОстатки.СуммаВзаиморасчетовОстаток * ВЫБОР
	|		КОГДА ЕСТЬNULL(КурсыВалют.Курс, 0) > 0
	|				И ЕСТЬNULL(КурсыВалют.Кратность, 0) > 0
	|			ТОГДА КурсыВалют.Курс / КурсыВалют.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ                                                                  КАК СуммаРегл,
	|	-ВзаиморасчетыСКонтрагентамиОстатки.СуммаУпрОстаток					   КАК СуммаУпр,
	|	ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента КАК РасчетныйДокумент,
	|	ВЫБОР
	|		КОГДА ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента.Номер = """"
	|			ТОГДА ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента.Код
	|		ИНАЧЕ ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента.Номер
	|	КОНЕЦ КАК НомерРасчетногоДокумента,
	|	ВЫБОР
	|		КОГДА ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента.Дата = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА &ДатаОстатков
	|		ИНАЧЕ ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента.Дата
	|	КОНЕЦ КАК ДатаРасчетногоДокумента
	|	
	|ПОМЕСТИТЬ ОстаткиВзаиморасчетов
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыСКонтрагентами.Остатки(
	|			&ДатаОстатков,
	|			ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПокупателем)
	|				И (ДоговорКонтрагента.ВедениеВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоДоговоруВЦелом)
	|					ИЛИ ДоговорКонтрагента.ВедениеВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоСчетам)
	|					ИЛИ ДоговорКонтрагента.ВедениеВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоЗаказам)
	|						И (НЕ Сделка ССЫЛКА Документ.ЗаказПокупателя))) КАК ВзаиморасчетыСКонтрагентамиОстатки
	|	
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, ) КАК КурсыВалют
	|		ПО ВзаиморасчетыСКонтрагентамиОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов = КурсыВалют.Валюта
	|	
	|ГДЕ
	|	ВзаиморасчетыСКонтрагентамиОстатки.СуммаВзаиморасчетовОстаток < 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиВзаиморасчетов.Организация КАК Организация,
	|	ОстаткиВзаиморасчетов.Контрагент КАК Контрагент,
	|	ОстаткиВзаиморасчетов.Партнер,
	|	СУММА(ОстаткиВзаиморасчетов.Сумма) КАК Сумма,
	|	СУММА(ОстаткиВзаиморасчетов.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ОстаткиВзаиморасчетов.СуммаУпр) КАК СуммаУпр,
	|	ОстаткиВзаиморасчетов.ВалютаВзаиморасчетов,
	|	ОстаткиВзаиморасчетов.РасчетныйДокумент КАК ОбъектРасчетов,
	|	ОстаткиВзаиморасчетов.НомерРасчетногоДокумента,
	|	ОстаткиВзаиморасчетов.ДатаРасчетногоДокумента
	|ИЗ
	|	ОстаткиВзаиморасчетов КАК ОстаткиВзаиморасчетов
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиВзаиморасчетов.Организация,
	|	ОстаткиВзаиморасчетов.Контрагент,
	|	ОстаткиВзаиморасчетов.Партнер,
	|	ОстаткиВзаиморасчетов.ВалютаВзаиморасчетов,
	|	ОстаткиВзаиморасчетов.РасчетныйДокумент,
	|	ОстаткиВзаиморасчетов.НомерРасчетногоДокумента,
	|	ОстаткиВзаиморасчетов.ДатаРасчетногоДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Контрагент
	|ИТОГИ ПО
	|	Организация
	|АВТОУПОРЯДОЧИВАНИЕ");
	
Если Параметры.Организации.Количество() > 0 Тогда
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ", 
								"Организация В (&МассивОрганизаций) И ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ");
	Запрос.УстановитьПараметр("МассивОрганизаций", Параметры.Организации);
КонецЕсли;	

Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(Параметры.ДатаОстатков));

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("Организация");
ВыборкаДанных.Колонки.Добавить("РасчетыСПартнерами");

РезультатыЗапроса = Запрос.Выполнить();
ВыборкаПоОрганизациям = РезультатыЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоОрганизациям.Следующий() Цикл
	
	НоваяСтрока = ВыборкаДанных.Добавить();
	НоваяСтрока.Дата        = КонецДня(Параметры.ДатаОстатков); 
	НоваяСтрока.Организация = ВыборкаПоОрганизациям.Организация; 
	
	РасчетыСПартнерами = Неопределено;
	Выполнить(Алгоритмы.ТаблицаРасчетовСПартнерами);
	НоваяСтрока.РасчетыСПартнерами = РасчетыСПартнерами;
	
	ВыборкаДетальныеЗаписи = ВыборкаПоОрганизациям.Выбрать(); 
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрокаРасчетыСПартнерами = НоваяСтрока.РасчетыСПартнерами.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРасчетыСПартнерами, ВыборкаДетальныеЗаписи);
	КонецЦикла;
КонецЦикла;

Выполнить(Алгоритмы.ОчисткаВыборки);
