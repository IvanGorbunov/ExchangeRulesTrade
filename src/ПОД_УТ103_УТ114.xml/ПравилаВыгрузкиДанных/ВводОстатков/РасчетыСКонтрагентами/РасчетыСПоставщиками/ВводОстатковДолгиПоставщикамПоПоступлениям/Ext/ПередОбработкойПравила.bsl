Запрос = Новый Запрос("ВЫБРАТЬ
                      |	&ДатаОстатков КАК ДатаПлатежа,
                      |	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Организация КАК Организация,
                      |	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом КАК ДокументРасчетовСКонтрагентом,
                      |	ВЫБОР
                      |		КОГДА ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.ПоступлениеТоваровУслуг
                      |			ТОГДА ВЫРАЗИТЬ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом КАК Документ.ПоступлениеТоваровУслуг).Дата
                      |		КОГДА ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.ПоступлениеТоваровУслугВНТТ
                      |			ТОГДА ВЫРАЗИТЬ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом КАК Документ.ПоступлениеТоваровУслугВНТТ).Дата
                      |	КОНЕЦ КАК ДатаРасчетногоДокумента,
                      |	ВЫБОР
                      |		КОГДА ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.ПоступлениеТоваровУслуг
                      |			ТОГДА ВЫРАЗИТЬ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом КАК Документ.ПоступлениеТоваровУслуг).Номер
                      |		КОГДА ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом ССЫЛКА Документ.ПоступлениеТоваровУслугВНТТ
                      |			ТОГДА ВЫРАЗИТЬ(ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДокументРасчетовСКонтрагентом КАК Документ.ПоступлениеТоваровУслугВНТТ).Номер
                      |	КОНЕЦ КАК НомерРасчетногоДокумента,
                      |	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Контрагент КАК Партнер,
                      |	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.Контрагент КАК Контрагент,
                      |	-ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток КАК Сумма,
                      |	-ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток * ВЫБОР
                      |		КОГДА ЕСТЬNULL(КурсыВалют.Курс, 0) > 0
                      |				И ЕСТЬNULL(КурсыВалют.Кратность, 0) > 0
                      |			ТОГДА КурсыВалют.Курс / КурсыВалют.Кратность
                      |		ИНАЧЕ 0
                      |	КОНЕЦ КАК СуммаРегл,
                      |	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
                      |	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДоговорКонтрагента КАК ОбъектРасчетов
                      |ИЗ
                      |	РегистрНакопления.ВзаиморасчетыСКонтрагентамиПоДокументамРасчетов.Остатки(
                      |			&ДатаОстатков,
                      |			ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком)
                      |				И УпрУчет
                      |				И ДоговорКонтрагента.ВедениеВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоСчетам)
                      |				И (ДокументРасчетовСКонтрагентом ССЫЛКА Документ.ПоступлениеТоваровУслуг
                      |					ИЛИ ДокументРасчетовСКонтрагентом ССЫЛКА Документ.ПоступлениеТоваровУслугВНТТ)) КАК ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки
                      |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, ) КАК КурсыВалют
                      |		ПО ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов = КурсыВалют.Валюта
                      |ГДЕ
                      |	ВзаиморасчетыСКонтрагентамиПоДокументамРасчетовОстатки.СуммаВзаиморасчетовОстаток < 0
                      |
                      |УПОРЯДОЧИТЬ ПО
                      |	Контрагент
                      |ИТОГИ ПО
                      |	Организация
                      |АВТОУПОРЯДОЧИВАНИЕ");
	
Если Параметры.Организации.Количество() > 0 Тогда
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ", 
								"Организация В (&МассивОрганизаций) И ДоговорКонтрагента.ВидДоговора = ЗНАЧЕНИЕ");
	Запрос.УстановитьПараметр("МассивОрганизаций", Параметры.Организации);
КонецЕсли;	

Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(Параметры.ДатаОстатков));

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("Организация");
ВыборкаДанных.Колонки.Добавить("РасчетыСПартнерами");

РезультатыЗапроса = Запрос.Выполнить();
ВыборкаПоОрганизациям = РезультатыЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоОрганизациям.Следующий() Цикл
	
	НоваяСтрока = ВыборкаДанных.Добавить();
	НоваяСтрока.Дата        = КонецДня(Параметры.ДатаОстатков); 
	НоваяСтрока.Организация = ВыборкаПоОрганизациям.Организация; 
	
	РасчетыСПартнерами = Неопределено;
	Выполнить(Алгоритмы.ТаблицаРасчетовСПартнерами);
	РасчетыСПартнерами.Колонки.Добавить("ДокументРасчетовСКонтрагентом");
	НоваяСтрока.РасчетыСПартнерами = РасчетыСПартнерами;
	
	ВыборкаДетальныеЗаписи = ВыборкаПоОрганизациям.Выбрать(); 
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		НоваяСтрокаРасчетыСПартнерами = НоваяСтрока.РасчетыСПартнерами.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаРасчетыСПартнерами, ВыборкаДетальныеЗаписи);
		НоваяСтрокаРасчетыСПартнерами.СуммаУпр = МодульВалютногоУчета.ПересчитатьИзВалютыВВалюту(ВыборкаДетальныеЗаписи.СуммаРегл,
			Параметры.ВалютаРеглУчета, Параметры.ВалютаУпрУчета, 1, Параметры.КурсВУУ.Курс, 1, Параметры.КурсВУУ.Кратность);
			
	КонецЦикла;
	
КонецЦикла;

Выполнить(Алгоритмы.ОчисткаВыборки);
