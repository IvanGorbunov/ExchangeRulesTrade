Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОстаткиВозвратнойТары.Организация                                                             КАК Организация,
	|	ОстаткиВозвратнойТары.Контрагент                                                              КАК Контрагент,
	|	ОстаткиВозвратнойТары.Номенклатура                                                            КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)                                                       КАК СтавкаНДС,
	|	
	|	СУММА(ЕСТЬNULL(ОстаткиВозвратнойТары.КоличествоОстаток, 0)
	|		* ЕСТЬNULL(ОстаткиВозвратнойТары.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1))    КАК Количество,
	|	
	|	СУММА(ЕСТЬNULL(ОстаткиВозвратнойТары.СуммаВзаиморасчетовОстаток, 0))                          КАК Сумма,
	|	СУММА(ЕСТЬNULL(ОстаткиВозвратнойТары.СуммаВзаиморасчетовОстаток, 0))                          КАК СуммаБезНДС,
	|	СУММА(ЕСТЬNULL(ОстаткиВозвратнойТары.СуммаВзаиморасчетовОстаток, 0) * ВЫБОР
	|		КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) > 0
	|			И ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) > 0
	|			ТОГДА КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                                                                        КАК СуммаРегл,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиВозвратнойТары.КоличествоОстаток, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ОстаткиВозвратнойТары.СуммаВзаиморасчетовОстаток, 0)
	|			/ ОстаткиВозвратнойТары.КоличествоОстаток
	|			/ ЕСТЬNULL(ОстаткиВозвратнойТары.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
	|	КОНЕЦ                                                                                         КАК Цена
	|	
	|ИЗ
	|	РегистрНакопления.ТоварыПереданные.Остатки(
	|			&ДатаОстатков,
	|			СтатусПередачи = ЗНАЧЕНИЕ(Перечисление.СтатусыПолученияПередачиТоваров.ВозвратнаяТара)
	|				И Номенклатура.ВидНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)) КАК ОстаткиВозвратнойТары
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, Валюта = &ВалютаУпрУчета) КАК КурсыВалютСрезПоследних
	|	ПО (ИСТИНА)
	|	
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиВозвратнойТары.Организация,
	|	ОстаткиВозвратнойТары.Контрагент,
	|	ОстаткиВозвратнойТары.Номенклатура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиВозвратнойТары.КоличествоОстаток, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ОстаткиВозвратнойТары.СуммаВзаиморасчетовОстаток, 0)
	|			/ ОстаткиВозвратнойТары.КоличествоОстаток
	|			/ ЕСТЬNULL(ОстаткиВозвратнойТары.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
	|	КОНЕЦ
	|	
	|ИТОГИ ПО
	|	Организация,
	|	Контрагент
	|	
	|АВТОУПОРЯДОЧИВАНИЕ");
	
Если Параметры.Организации.Количество() > 0 Тогда
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПО (ИСТИНА)",
		"ПО (ИСТИНА)
		| ГДЕ
		|	ОстаткиВозвратнойТары.Организация В (&МассивОрганизаций) ");
	Запрос.УстановитьПараметр("МассивОрганизаций", Параметры.Организации);	
КонецЕсли;	

Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(Параметры.ДатаОстатков));
Запрос.УстановитьПараметр("ВалютаУпрУчета", Константы.ВалютаУправленческогоУчета.Получить());

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("Организация");
ВыборкаДанных.Колонки.Добавить("Партнер");
ВыборкаДанных.Колонки.Добавить("Валюта");
ВыборкаДанных.Колонки.Добавить("Товары");

РезультатЗапроса = Запрос.Выполнить();
ВыборкаПоОрганизациям = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоОрганизациям.Следующий() Цикл
	
	ВыборкаПоКонтрагентам = ВыборкаПоОрганизациям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоКонтрагентам.Следующий() Цикл
		
		НоваяСтрока = ВыборкаДанных.Добавить();
		НоваяСтрока.Дата        = КонецДня(Параметры.ДатаОстатков);
		НоваяСтрока.Организация = ВыборкаПоОрганизациям.Организация;
		НоваяСтрока.Партнер     = ВыборкаПоКонтрагентам.Контрагент;
		НоваяСтрока.Валюта      = Константы.ВалютаУправленческогоУчета.Получить();
		
		НоваяСтрока.Товары = Новый ТаблицаЗначений;
		НоваяСтрока.Товары.Колонки.Добавить("Номенклатура");
		НоваяСтрока.Товары.Колонки.Добавить("Количество");
		НоваяСтрока.Товары.Колонки.Добавить("КоличествоУпаковок");
		НоваяСтрока.Товары.Колонки.Добавить("Цена");
		НоваяСтрока.Товары.Колонки.Добавить("Сумма");
		НоваяСтрока.Товары.Колонки.Добавить("СуммаБезНДС");
		НоваяСтрока.Товары.Колонки.Добавить("СуммаРегл");
		НоваяСтрока.Товары.Колонки.Добавить("СтавкаНДС");
		
		ВыборкаДетальныеЗаписи = ВыборкаПоКонтрагентам.Выбрать(); 
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Если ВыборкаДетальныеЗаписи.Количество < 0 Тогда
				СтрокаСообщения = НСтр("ru = 'Обнаружен отрицательный остаток по возвратной таре ""%1"" при выгрузке по правилу: ""%2""'");
				СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1", ВыборкаДетальныеЗаписи.Номенклатура);
				СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2", НСтр("ru = 'Возвратная тара, переданная клиентам'"));
				ЗаписатьВПротоколВыполнения(СтрокаСообщения,,Ложь);
				Продолжить;
			КонецЕсли;
			
			НоваяСтрокаТовары = НоваяСтрока.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, ВыборкаДетальныеЗаписи);
			НоваяСтрокаТовары.КоличествоУпаковок = ВыборкаДетальныеЗаписи.Количество;
			
		КонецЦикла;
		
		Если НоваяСтрока.Товары.Количество() = 0 Тогда
			ВыборкаДанных.Удалить(НоваяСтрока);
		КонецЕсли;
		
	КонецЦикла;
КонецЦикла;
