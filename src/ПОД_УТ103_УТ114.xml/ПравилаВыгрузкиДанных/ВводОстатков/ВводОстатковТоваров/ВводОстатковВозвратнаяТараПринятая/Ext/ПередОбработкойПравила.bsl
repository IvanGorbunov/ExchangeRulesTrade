Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ОстаткиВозвратнойТары.ДокументОприходования.Организация,
	|		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))                                            КАК Организация,
	|	
	|	ЕСТЬNULL(ОстаткиВозвратнойТары.ДокументОприходования.ДоговорКонтрагента.Владелец,
	|		ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка))                                            КАК Контрагент,
	|	
	|	ОстаткиВозвратнойТары.Склад                                                                   КАК Склад,
	|	ОстаткиВозвратнойТары.Номенклатура                                                            КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Перечисление.СтавкиНДС.БезНДС)                                                       КАК СтавкаНДС,
	|	
	|	СУММА(ЕСТЬNULL(ОстаткиВозвратнойТары.КоличествоОстаток, 0)
	|		* ЕСТЬNULL(ОстаткиВозвратнойТары.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1))    КАК Количество,
	|	
	|	СУММА(ЕСТЬNULL(ОстаткиВозвратнойТары.СтоимостьОстаток, 0))                                    КАК Сумма,
	|	СУММА(ЕСТЬNULL(ОстаткиВозвратнойТары.СтоимостьОстаток, 0))                                    КАК СуммаБезНДС,
	|	СУММА(ЕСТЬNULL(ОстаткиВозвратнойТары.СтоимостьОстаток, 0) * ВЫБОР
	|		КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) > 0
	|			И ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) > 0
	|			ТОГДА КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                                                                        КАК СуммаРегл,
	|	
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиВозвратнойТары.КоличествоОстаток, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ОстаткиВозвратнойТары.СтоимостьОстаток, 0)
	|			/ ОстаткиВозвратнойТары.КоличествоОстаток
	|			/ ЕСТЬNULL(ОстаткиВозвратнойТары.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
	|	КОНЕЦ                                                                                         КАК Цена
	|	
	|ИЗ
	|	РегистрНакопления.ПартииТоваровНаСкладах.Остатки(
	|			&ДатаОстатков,
	|			СтатусПартии = ЗНАЧЕНИЕ(Перечисление.СтатусыПартийТоваров.ВозвратнаяТара)
	|				И Номенклатура.ВидНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)) КАК ОстаткиВозвратнойТары
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, Валюта = &ВалютаУпрУчета) КАК КурсыВалютСрезПоследних
	|	ПО (ИСТИНА)
	|	
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(ОстаткиВозвратнойТары.ДокументОприходования.Организация,
	|		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)),
	|	ЕСТЬNULL(ОстаткиВозвратнойТары.ДокументОприходования.ДоговорКонтрагента.Владелец,
	|		ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)),
	|	ОстаткиВозвратнойТары.Склад,
	|	ОстаткиВозвратнойТары.Номенклатура,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОстаткиВозвратнойТары.КоличествоОстаток, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ОстаткиВозвратнойТары.СтоимостьОстаток, 0)
	|			/ ОстаткиВозвратнойТары.КоличествоОстаток
	|			/ ЕСТЬNULL(ОстаткиВозвратнойТары.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
	|	КОНЕЦ
	|	
	|ИТОГИ ПО
	|	Организация,
	|	Контрагент,
	|	Склад
	|	
	|АВТОУПОРЯДОЧИВАНИЕ");
	
Если Параметры.Организации.Количество() > 0 Тогда
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПО (ИСТИНА)",
		"ПО (ИСТИНА)
		| ГДЕ
		|	ЕСТЬNULL(ОстаткиВозвратнойТары.ДокументОприходования.Организация,
		|	ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) В (&МассивОрганизаций) ");
	Запрос.УстановитьПараметр("МассивОрганизаций", Параметры.Организации);	
КонецЕсли;	

Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(Параметры.ДатаОстатков));
Запрос.УстановитьПараметр("ВалютаУпрУчета", Константы.ВалютаУправленческогоУчета.Получить());

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("Организация");
ВыборкаДанных.Колонки.Добавить("Склад");
ВыборкаДанных.Колонки.Добавить("Партнер");
ВыборкаДанных.Колонки.Добавить("Контрагент");
ВыборкаДанных.Колонки.Добавить("Валюта");
ВыборкаДанных.Колонки.Добавить("Товары");

РезультатЗапроса = Запрос.Выполнить();
ВыборкаПоОрганизациям = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоОрганизациям.Следующий() Цикл
	
	ВыборкаПоКонтрагентам = ВыборкаПоОрганизациям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоКонтрагентам.Следующий() Цикл
		
		ВыборкаПоСкладам = ВыборкаПоКонтрагентам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоСкладам.Следующий() Цикл
			
			НоваяСтрока = ВыборкаДанных.Добавить();
			НоваяСтрока.Дата        = КонецДня(Параметры.ДатаОстатков);
			НоваяСтрока.Организация = ВыборкаПоОрганизациям.Организация;
			НоваяСтрока.Склад       = ВыборкаПоСкладам.Склад;
			НоваяСтрока.Партнер     = ВыборкаПоКонтрагентам.Контрагент;
			НоваяСтрока.Контрагент  = ВыборкаПоКонтрагентам.Контрагент;
			НоваяСтрока.Валюта      = Константы.ВалютаУправленческогоУчета.Получить();
			
			НоваяСтрока.Товары = Новый ТаблицаЗначений;
			НоваяСтрока.Товары.Колонки.Добавить("Номенклатура");
			НоваяСтрока.Товары.Колонки.Добавить("Количество");
			НоваяСтрока.Товары.Колонки.Добавить("КоличествоУпаковок");
			НоваяСтрока.Товары.Колонки.Добавить("Цена");
			НоваяСтрока.Товары.Колонки.Добавить("Сумма");
			НоваяСтрока.Товары.Колонки.Добавить("СуммаБезНДС");
			НоваяСтрока.Товары.Колонки.Добавить("СуммаРегл");
			НоваяСтрока.Товары.Колонки.Добавить("СтавкаНДС");
			
			ВыборкаДетальныеЗаписи = ВыборкаПоСкладам.Выбрать(); 
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				Если ВыборкаДетальныеЗаписи.Количество < 0 Тогда
					СтрокаСообщения = НСтр("ru = 'Обнаружен отрицательный остаток на складе ""%1"" по возвратной таре ""%2"" при выгрузке по правилу: ""%3""'");
					СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%1", ВыборкаПоСкладам.Склад);
					СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2", ВыборкаДетальныеЗаписи.Номенклатура);
					СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%3", НСтр("ru = 'Возвратная тара, принятая от поставщиков'"));
					
					Если Не ЗначениеЗаполнено(ВыборкаПоСкладам.Склад) Тогда
						СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "на складе """" ", "");
					КонецЕсли;
					
					ЗаписатьВПротоколВыполнения(СтрокаСообщения,,Ложь);
					Продолжить;
				КонецЕсли;
				
				НоваяСтрокаТовары = НоваяСтрока.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, ВыборкаДетальныеЗаписи);
				НоваяСтрокаТовары.КоличествоУпаковок = ВыборкаДетальныеЗаписи.Количество;
				
			КонецЦикла;
			
			Если НоваяСтрока.Товары.Количество() = 0 Тогда
				ВыборкаДанных.Удалить(НоваяСтрока);
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
КонецЦикла;
