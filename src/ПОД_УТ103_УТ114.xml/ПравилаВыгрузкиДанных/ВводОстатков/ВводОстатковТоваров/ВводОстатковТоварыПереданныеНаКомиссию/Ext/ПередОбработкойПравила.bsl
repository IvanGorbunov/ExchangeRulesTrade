Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ПартииТоваровПереданныеОстатки.ДокументПередачи.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	|	ЕСТЬNULL(ПартииТоваровПереданныеОстатки.ДоговорКонтрагента.Владелец, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК Контрагент,
	|	ЕСТЬNULL(ПартииТоваровПереданныеОстатки.ДоговорКонтрагента.ВалютаВзаиморасчетов, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК ВалютаВзаиморасчетов,
	|	ПартииТоваровПереданныеОстатки.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ПартииТоваровПереданныеОстатки.Номенклатура КАК Номенклатура,
	|	ПартииТоваровПереданныеОстатки.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	ПартииТоваровПереданныеОстатки.ХарактеристикаНоменклатуры КАК Характеристика,
	|	ЕСТЬNULL(ТоварыПереданныеОстатки.КоличествоОстаток, 0) * ЕСТЬNULL(ПартииТоваровПереданныеОстатки.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1) КАК Количество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПартииТоваровПереданныеОстатки.КоличествоОстаток, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ПартииТоваровПереданныеОстатки.СтоимостьОстаток, 0)
	|			* ЕСТЬNULL(ТоварыПереданныеОстатки.КоличествоОстаток, 0)
	|			/ ПартииТоваровПереданныеОстатки.КоличествоОстаток
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПартииТоваровПереданныеОстатки.КоличествоОстаток, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ПартииТоваровПереданныеОстатки.СтоимостьОстаток, 0)
	|			* ЕСТЬNULL(ТоварыПереданныеОстатки.КоличествоОстаток, 0)
	|			/ ПартииТоваровПереданныеОстатки.КоличествоОстаток
	|	КОНЕЦ * ВЫБОР
	|		КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) > 0
	|			И ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) > 0
	|			ТОГДА КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСНДСРегл,
	|	ЕСТЬNULL(ТоварыПереданныеОстатки.СерияНоменклатуры.НомерГТД, ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)) КАК НомерГТД,
	|	ЕСТЬNULL(ТоварыПереданныеОстатки.СерияНоменклатуры.СтранаПроисхождения, ЗНАЧЕНИЕ(Справочник.КлассификаторСтранМира.ПустаяСсылка)) КАК СтранаПроисхождения,
	|	ЕСТЬNULL(ТоварыПереданныеОстатки.СерияНоменклатуры.ПометкаУдаления, ЛОЖЬ) КАК ПометкаУдаления
	|ИЗ
	|	РегистрНакопления.ПартииТоваровПереданные.Остатки(&ДатаОстатков, СтатусПередачи = ЗНАЧЕНИЕ(Перечисление.СтатусыПолученияПередачиТоваров.НаКомиссию)) КАК ПартииТоваровПереданныеОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыПереданные.Остатки КАК ТоварыПереданныеОстатки
	|		ПО ПартииТоваровПереданныеОстатки.Номенклатура = ТоварыПереданныеОстатки.Номенклатура
	|			И ПартииТоваровПереданныеОстатки.ХарактеристикаНоменклатуры = ТоварыПереданныеОстатки.ХарактеристикаНоменклатуры
	|			И ПартииТоваровПереданныеОстатки.ДоговорКонтрагента = ТоварыПереданныеОстатки.ДоговорКонтрагента
	|			И ПартииТоваровПереданныеОстатки.ДокументПередачи.Организация = ТоварыПереданныеОстатки.Организация
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, Валюта = &ВалютаУпрУчета) КАК КурсыВалютСрезПоследних
	|	ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПартииТоваровПереданныеОстатки.Номенклатура.Наименование,
	|	ПартииТоваровПереданныеОстатки.ХарактеристикаНоменклатуры.Наименование
	|ИТОГИ ПО
	|	Организация,
	|	Контрагент,
	|	ДоговорКонтрагента");
	
Если Параметры.Организации.Количество() > 0 Тогда
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПО (ИСТИНА)",
		"ПО (ИСТИНА)
		| ГДЕ
		|	ЕСТЬNULL(ПартииТоваровПереданныеОстатки.ДокументПередачи.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) В (&МассивОрганизаций) ");
	Запрос.УстановитьПараметр("МассивОрганизаций", Параметры.Организации);	
КонецЕсли;	

Запрос.УстановитьПараметр("ДатаОстатков", КонецДня(ТекущаяДата()));
Запрос.УстановитьПараметр("ВалютаУпрУчета", Константы.ВалютаУправленческогоУчета.Получить());

ВыборкаДанных = Новый ТаблицаЗначений;
ВыборкаДанных.Колонки.Добавить("Дата");
ВыборкаДанных.Колонки.Добавить("Организация");
ВыборкаДанных.Колонки.Добавить("Партнер");
ВыборкаДанных.Колонки.Добавить("СоглашениеСКомиссионером");
ВыборкаДанных.Колонки.Добавить("Валюта");
ВыборкаДанных.Колонки.Добавить("Товары");

РезультатыЗапроса = Запрос.Выполнить();
ВыборкаПоОрганизациям = РезультатыЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаПоОрганизациям.Следующий() Цикл
	
	ВыборкаПоКонтрагентам = ВыборкаПоОрганизациям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоКонтрагентам.Следующий() Цикл
		
		ВыборкаПоДоговорамКонтрагента = ВыборкаПоКонтрагентам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоДоговорамКонтрагента.Следующий() Цикл
			
			НоваяСтрока = ВыборкаДанных.Добавить();
			НоваяСтрока.Дата                     = КонецДня(ТекущаяДата()); 
			НоваяСтрока.Организация              = ВыборкаПоОрганизациям.Организация; 
			НоваяСтрока.Партнер                  = ВыборкаПоКонтрагентам.Контрагент; 
			НоваяСтрока.СоглашениеСКомиссионером = ВыборкаПоДоговорамКонтрагента.ДоговорКонтрагента; 
			НоваяСтрока.Валюта                   = ВыборкаПоДоговорамКонтрагента.ВалютаВзаиморасчетов; 
			
			НоваяСтрока.Товары = Новый ТаблицаЗначений;
			НоваяСтрока.Товары.Колонки.Добавить("Номенклатура");
			НоваяСтрока.Товары.Колонки.Добавить("Характеристика");
			НоваяСтрока.Товары.Колонки.Добавить("Количество");
			НоваяСтрока.Товары.Колонки.Добавить("КоличествоУпаковок");
			НоваяСтрока.Товары.Колонки.Добавить("СтавкаНДС");
			НоваяСтрока.Товары.Колонки.Добавить("Сумма");
			НоваяСтрока.Товары.Колонки.Добавить("СуммаБезНДС");
			НоваяСтрока.Товары.Колонки.Добавить("СуммаНДС");
			НоваяСтрока.Товары.Колонки.Добавить("СуммаРегл");
			НоваяСтрока.Товары.Колонки.Добавить("НДСРегл");
			НоваяСтрока.Товары.Колонки.Добавить("Цена");
			НоваяСтрока.Товары.Колонки.Добавить("НомерГТД");
			
			ВыборкаДетальныеЗаписи = ВыборкаПоДоговорамКонтрагента.Выбрать(); 
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				
				СтрокаСообщения = "";
				Если ВыборкаДетальныеЗаписи.Количество < 0 Тогда
					СтрокаСообщения = НСтр("ru = 'Обнаружен отрицательный остаток по товару ""%2"" при выгрузке по правилу: ""%3""'");
				ИначеЕсли ВыборкаДетальныеЗаписи.Сумма <= 0 ИЛИ ВыборкаДетальныеЗаписи.Количество = 0 Тогда
					СтрокаСообщения = НСтр("ru = 'Обнаружен некорректный суммовой остаток по товару ""%2"" при выгрузке по правилу: ""%3""'");
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаСообщения) Тогда
					СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%2", ВыборкаДетальныеЗаписи.Номенклатура);
					СтрокаСообщения = СтрЗаменить(СтрокаСообщения, "%3", НСтр("ru = 'Товары, переданные на комиссию'"));
					
					ЗаписатьВПротоколВыполнения(СтрокаСообщения,,Ложь);
					Продолжить;
				КонецЕсли;
				
				НоваяСтрокаТовары = НоваяСтрока.Товары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТовары, ВыборкаДетальныеЗаписи);
				НоваяСтрокаТовары.Цена = ВыборкаДетальныеЗаписи.Сумма / ВыборкаДетальныеЗаписи.Количество;
				НоваяСтрокаТовары.КоличествоУпаковок = ВыборкаДетальныеЗаписи.Количество;
				
				СтавкаНДСЧислом = УчетНДС.ПолучитьСтавкуНДС(ВыборкаДетальныеЗаписи.СтавкаНДС) / 100;
				
				НоваяСтрокаТовары.СуммаНДС    = НоваяСтрокаТовары.Сумма / (СтавкаНДСЧислом + 1) * СтавкаНДСЧислом;
				НоваяСтрокаТовары.НДСРегл     = ВыборкаДетальныеЗаписи.СуммаСНДСРегл / (СтавкаНДСЧислом + 1) * СтавкаНДСЧислом;
				НоваяСтрокаТовары.СуммаБезНДС = НоваяСтрокаТовары.Сумма - НоваяСтрокаТовары.СуммаНДС;
				НоваяСтрокаТовары.СуммаРегл   = ВыборкаДетальныеЗаписи.СуммаСНДСРегл - НоваяСтрокаТовары.НДСРегл;
				
				Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.НомерГТД) Тогда
					НоваяСтрокаТовары.НомерГТД = Новый Структура;
					НоваяСтрокаТовары.НомерГТД.Вставить("Владелец", ВыборкаДетальныеЗаписи.Номенклатура);
					НоваяСтрокаТовары.НомерГТД.Вставить("СтранаПроисхождения", ВыборкаДетальныеЗаписи.СтранаПроисхождения);
					НоваяСтрокаТовары.НомерГТД.Вставить("ПометкаУдаления", ВыборкаДетальныеЗаписи.ПометкаУдаления);
					НоваяСтрокаТовары.НомерГТД.Вставить("Код", ВыборкаДетальныеЗаписи.НомерГТД);
				КонецЕсли;
				
			КонецЦикла;
			
			Если НоваяСтрока.Товары.Количество() = 0 Тогда
				ВыборкаДанных.Удалить(НоваяСтрока);
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
КонецЦикла;
