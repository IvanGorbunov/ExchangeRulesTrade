ВЫБРАТЬ
	НДСПартииТоваровОстатки.Номенклатура КАК Номенклатура,
	НДСПартииТоваровОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	НДСПартииТоваровОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
	НДСПартииТоваровОстатки.Склад КАК Склад,
	НДСПартииТоваровОстатки.СтавкаНДС КАК СтавкаНДС,
	СУММА(НДСПартииТоваровОстатки.СтоимостьОстаток) КАК СтоимостьОстаток,
	СУММА(НДСПартииТоваровОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	СУММА(НДСПартииТоваровОстатки.НДСОстаток) КАК НДСОстаток,
	НДСПартииТоваровОстатки.Организация КАК Организация,
	НДСПартииТоваровОстатки.СчетФактура КАК СчетФактура
ПОМЕСТИТЬ НДСПоПартиям
ИЗ
	РегистрНакопления.НДСПартииТоваров.Остатки(&ДатаОстатков, ) КАК НДСПартииТоваровОстатки

СГРУППИРОВАТЬ ПО
	НДСПартииТоваровОстатки.СтавкаНДС,
	НДСПартииТоваровОстатки.Организация,
	НДСПартииТоваровОстатки.ХарактеристикаНоменклатуры,
	НДСПартииТоваровОстатки.СчетФактура,
	НДСПартииТоваровОстатки.Номенклатура,
	НДСПартииТоваровОстатки.Склад,
	НДСПартииТоваровОстатки.СерияНоменклатуры

ИНДЕКСИРОВАТЬ ПО
	Организация,
	Номенклатура,
	СчетФактура,
	ХарактеристикаНоменклатуры,
	СерияНоменклатуры,
	Склад
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЕСТЬNULL(ОстаткиТоваров.ДокументОприходования.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	ОстаткиТоваров.Склад КАК Склад,
	ОстаткиТоваров.Номенклатура КАК Номенклатура,
	ОстаткиТоваров.ДокументОприходования КАК ДокументОприходования,
	ОстаткиТоваров.Номенклатура.ВестиУчетПоСериям КАК ВестиУчетПоСериям,
	ОстаткиТоваров.ХарактеристикаНоменклатуры КАК Характеристика,
	ОстаткиТоваров.Качество КАК Качество,
	ОстаткиТоваров.СерияНоменклатуры КАК Серия,
	ОстаткиТоваров.СерияНоменклатуры.НомерГТД КАК НомерГТД,
	ОстаткиТоваров.СерияНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	ОстаткиТоваров.СерияНоменклатуры.ПометкаУдаления КАК ПометкаУдаления,
	СУММА(ЕСТЬNULL(ОстаткиТоваров.КоличествоОстаток, 0) * ЕСТЬNULL(ОстаткиТоваров.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)) КАК Количество,
	СУММА(ЕСТЬNULL(ОстаткиТоваров.СтоимостьОстаток, 0)) КАК СуммаИтогоУпр
ПОМЕСТИТЬ ОстаткиТоваровПоПартиям
ИЗ
	РегистрНакопления.ПартииТоваровНаСкладах.Остатки(
			&ДатаОстатков,
			СтатусПартии <> ЗНАЧЕНИЕ(Перечисление.СтатусыПартийТоваров.НаКомиссию)
				И СтатусПартии <> ЗНАЧЕНИЕ(Перечисление.СтатусыПартийТоваров.ВозвратнаяТара)
				И Номенклатура.ВидНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)) КАК ОстаткиТоваров

СГРУППИРОВАТЬ ПО
	ОстаткиТоваров.Номенклатура,
	ОстаткиТоваров.Склад,
	ОстаткиТоваров.ХарактеристикаНоменклатуры,
	ОстаткиТоваров.Качество,
	ЕСТЬNULL(ОстаткиТоваров.ДокументОприходования.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)),
	ОстаткиТоваров.СерияНоменклатуры,
	ОстаткиТоваров.СерияНоменклатуры.НомерГТД,
	ОстаткиТоваров.СерияНоменклатуры.СтранаПроисхождения,
	ОстаткиТоваров.СерияНоменклатуры.ПометкаУдаления,
	ОстаткиТоваров.Номенклатура.ВестиУчетПоСериям,
	ОстаткиТоваров.ДокументОприходования

ИНДЕКСИРОВАТЬ ПО
	Организация
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ОстаткиТоваровПоПартиям.Организация КАК Организация,
	ОстаткиТоваровПоПартиям.Склад КАК Склад,
	ОстаткиТоваровПоПартиям.Номенклатура КАК Номенклатура,
	ОстаткиТоваровПоПартиям.ДокументОприходования КАК ДокументОприходования,
	ОстаткиТоваровПоПартиям.ВестиУчетПоСериям КАК ВестиУчетПоСериям,
	ОстаткиТоваровПоПартиям.Характеристика КАК Характеристика,
	ОстаткиТоваровПоПартиям.Качество КАК Качество,
	ОстаткиТоваровПоПартиям.Серия КАК Серия,
	ОстаткиТоваровПоПартиям.НомерГТД КАК НомерГТД,
	ОстаткиТоваровПоПартиям.СтранаПроисхождения КАК СтранаПроисхождения,
	ОстаткиТоваровПоПартиям.ПометкаУдаления КАК ПометкаУдаления,
	ОстаткиТоваровПоПартиям.Количество,
	ОстаткиТоваровПоПартиям.СуммаИтогоУпр,
	ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчетСрезПоследних.СложныйУчетНДС, ЛОЖЬ) КАК СложныйУчетНДС,
	ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчетСрезПоследних.ПартионныйУчетНДСвРазрезеСерийИХарактеристик, ЛОЖЬ) КАК УчетНДСВРазрезеСерийИХарактеристик,
	ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчетСрезПоследних.ПартионныйУчетНДСвРазрезеСкладов, ЛОЖЬ) КАК УчетНДСВРазрезеСкладов
ПОМЕСТИТЬ ОстаткиПоПартиямСУчетнойПолитикой
ИЗ
	ОстаткиТоваровПоПартиям КАК ОстаткиТоваровПоПартиям
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&ДатаОстатков, ) КАК УчетнаяПолитикаНалоговыйУчетСрезПоследних
		ПО ОстаткиТоваровПоПартиям.Организация = УчетнаяПолитикаНалоговыйУчетСрезПоследних.Организация

ИНДЕКСИРОВАТЬ ПО
	Номенклатура,
	Организация,
	ДокументОприходования,
	УчетНДСВРазрезеСерийИХарактеристик,
	УчетНДСВРазрезеСкладов
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ОстаткиПоПартиямСУчетнойПолитикой.СложныйУчетНДС КАК СложныйУчетНДС,
	ОстаткиПоПартиямСУчетнойПолитикой.Организация КАК Организация,
	ОстаткиПоПартиямСУчетнойПолитикой.Склад КАК Склад,
	
	ОстаткиПоПартиямСУчетнойПолитикой.ДокументОприходования       КАК ДокументОприходования,
	ОстаткиПоПартиямСУчетнойПолитикой.ДокументОприходования.Дата  КАК ДатаДокументаОприходования,
	ОстаткиПоПартиямСУчетнойПолитикой.ДокументОприходования.Номер КАК НомерДокументаОприходования,
	
	МАКСИМУМ(ЕСТЬNULL(ОстаткиПоПартиямСУчетнойПолитикой.ДокументОприходования.Контрагент,
		ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)))                                           КАК Партнер,
	МАКСИМУМ(ЕСТЬNULL(ОстаткиПоПартиямСУчетнойПолитикой.ДокументОприходования.ДоговорКонтрагента,
		ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)))                                  КАК Договор,
	
	ОстаткиПоПартиямСУчетнойПолитикой.Номенклатура КАК Номенклатура,
	ОстаткиПоПартиямСУчетнойПолитикой.ВестиУчетПоСериям,
	ОстаткиПоПартиямСУчетнойПолитикой.Характеристика,
	ОстаткиПоПартиямСУчетнойПолитикой.Качество,
	ОстаткиПоПартиямСУчетнойПолитикой.Серия,
	СУММА(ОстаткиПоПартиямСУчетнойПолитикой.Количество) КАК Количество,
	СУММА(ОстаткиПоПартиямСУчетнойПолитикой.СуммаИтогоУпр) КАК Сумма,
	ЕСТЬNULL(НДСПоПартиям.СтавкаНДС, ОстаткиПоПартиямСУчетнойПолитикой.Номенклатура.СтавкаНДС) КАК СтавкаНДС,
	СУММА(ЕСТЬNULL(НДСПоПартиям.НДСОстаток, 0)) КАК НДСРегл,
	СУММА(ЕСТЬNULL(НДСПоПартиям.НДСОстаток, 0) * ВЫБОР
			КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) > 0
					И ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) > 0
				ТОГДА КурсыВалютСрезПоследних.Кратность / КурсыВалютСрезПоследних.Курс
			ИНАЧЕ 0
		КОНЕЦ) КАК СуммаНДС,
	СУММА(ОстаткиПоПартиямСУчетнойПолитикой.СуммаИтогоУпр * ВЫБОР
			КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) > 0
					И ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) > 0
				ТОГДА КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность
			ИНАЧЕ 0
		КОНЕЦ) КАК СуммаСНДСРегл,
	ОстаткиПоПартиямСУчетнойПолитикой.НомерГТД,
	ОстаткиПоПартиямСУчетнойПолитикой.СтранаПроисхождения,
	КурсыВалютСрезПоследних.Курс,
	ОстаткиПоПартиямСУчетнойПолитикой.ПометкаУдаления
ИЗ
	ОстаткиПоПартиямСУчетнойПолитикой КАК ОстаткиПоПартиямСУчетнойПолитикой
		ЛЕВОЕ СОЕДИНЕНИЕ НДСПоПартиям КАК НДСПоПартиям
		ПО ОстаткиПоПартиямСУчетнойПолитикой.Номенклатура = НДСПоПартиям.Номенклатура
			И ОстаткиПоПартиямСУчетнойПолитикой.Организация = НДСПоПартиям.Организация
			И ОстаткиПоПартиямСУчетнойПолитикой.ДокументОприходования = НДСПоПартиям.СчетФактура
			И (НЕ ОстаткиПоПартиямСУчетнойПолитикой.УчетНДСВРазрезеСерийИХарактеристик
				ИЛИ ОстаткиПоПартиямСУчетнойПолитикой.Серия = НДСПоПартиям.СерияНоменклатуры)
			И (НЕ ОстаткиПоПартиямСУчетнойПолитикой.УчетНДСВРазрезеСкладов
				ИЛИ ОстаткиПоПартиямСУчетнойПолитикой.Склад = НДСПоПартиям.Склад)
			И (НЕ ОстаткиПоПартиямСУчетнойПолитикой.УчетНДСВРазрезеСерийИХарактеристик
				ИЛИ ОстаткиПоПартиямСУчетнойПолитикой.Характеристика = НДСПоПартиям.ХарактеристикаНоменклатуры)
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, Валюта = &ВалютаУпрУчета) КАК КурсыВалютСрезПоследних
		ПО (ИСТИНА)

СГРУППИРОВАТЬ ПО
	НДСПоПартиям.СтавкаНДС,
	ОстаткиПоПартиямСУчетнойПолитикой.Организация,
	ОстаткиПоПартиямСУчетнойПолитикой.Склад,
	ОстаткиПоПартиямСУчетнойПолитикой.Номенклатура,
	ОстаткиПоПартиямСУчетнойПолитикой.ДокументОприходования,
	ОстаткиПоПартиямСУчетнойПолитикой.ВестиУчетПоСериям,
	ОстаткиПоПартиямСУчетнойПолитикой.Характеристика,
	ОстаткиПоПартиямСУчетнойПолитикой.Качество,
	ОстаткиПоПартиямСУчетнойПолитикой.Серия,
	ОстаткиПоПартиямСУчетнойПолитикой.НомерГТД,
	ОстаткиПоПартиямСУчетнойПолитикой.СтранаПроисхождения,
	ОстаткиПоПартиямСУчетнойПолитикой.Количество,
	ОстаткиПоПартиямСУчетнойПолитикой.СуммаИтогоУпр,
	ОстаткиПоПартиямСУчетнойПолитикой.СложныйУчетНДС,
	КурсыВалютСрезПоследних.Курс,
	ОстаткиПоПартиямСУчетнойПолитикой.ПометкаУдаления,
	ЕСТЬNULL(НДСПоПартиям.СтавкаНДС, ОстаткиПоПартиямСУчетнойПолитикой.Номенклатура.СтавкаНДС)

УПОРЯДОЧИТЬ ПО
	Организация,
	Склад,
	СложныйУчетНДС,
	Номенклатура,
	ДокументОприходования

ИТОГИ ПО
	Организация,
	Склад,
	СложныйУчетНДС,
	ДокументОприходования
