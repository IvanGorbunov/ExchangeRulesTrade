ВЫБРАТЬ
	НДСПартииТоваровОстатки.Номенклатура КАК Номенклатура,
	НДСПартииТоваровОстатки.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
	НДСПартииТоваровОстатки.СерияНоменклатуры КАК СерияНоменклатуры,
	НДСПартииТоваровОстатки.СтавкаНДС КАК СтавкаНДС,
	СУММА(НДСПартииТоваровОстатки.СтоимостьОстаток) КАК СтоимостьОстаток,
	СУММА(НДСПартииТоваровОстатки.КоличествоОстаток) КАК КоличествоОстаток,
	СУММА(НДСПартииТоваровОстатки.НДСОстаток) КАК НДСОстаток,
	НДСПартииТоваровОстатки.Организация КАК Организация,
	НДСПартииТоваровОстатки.СчетФактура КАК СчетФактура
ПОМЕСТИТЬ НДСПоПартиям
ИЗ
	РегистрНакопления.НДСПартииТоваров.Остатки(&ДатаОстатков, ) КАК НДСПартииТоваровОстатки

СГРУППИРОВАТЬ ПО
	НДСПартииТоваровОстатки.СтавкаНДС,
	НДСПартииТоваровОстатки.Организация,
	НДСПартииТоваровОстатки.ХарактеристикаНоменклатуры,
	НДСПартииТоваровОстатки.СчетФактура,
	НДСПартииТоваровОстатки.Номенклатура,
	НДСПартииТоваровОстатки.СерияНоменклатуры

ИНДЕКСИРОВАТЬ ПО
	Организация,
	Номенклатура,
	СчетФактура,
	ХарактеристикаНоменклатуры,
	СерияНоменклатуры
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ЕСТЬNULL(ОстаткиТоваров.ДокументОприходования.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	ОстаткиТоваров.Номенклатура КАК Номенклатура,
	ОстаткиТоваров.ДокументОприходования КАК ДокументОприходования,
	ОстаткиТоваров.Номенклатура.ВестиУчетПоСериям КАК ВестиУчетПоСериям,
	ОстаткиТоваров.ХарактеристикаНоменклатуры КАК Характеристика,
	ОстаткиТоваров.Качество КАК Качество,
	ОстаткиТоваров.СерияНоменклатуры КАК Серия,
	ОстаткиТоваров.СерияНоменклатуры.НомерГТД КАК НомерГТД,
	ОстаткиТоваров.СерияНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	ОстаткиТоваров.СерияНоменклатуры.ПометкаУдаления КАК ПометкаУдаления,
	СУММА(ЕСТЬNULL(ОстаткиТоваров.КоличествоОстаток, 0) * ЕСТЬNULL(ОстаткиТоваров.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)) КАК Количество,
	СУММА(ЕСТЬNULL(ОстаткиТоваров.СтоимостьОстаток, 0)) КАК СуммаИтогоУпр
ПОМЕСТИТЬ ОстаткиТоваровПоПартиям
ИЗ
	РегистрНакопления.ПартииТоваровНаСкладах.Остатки(
			&ДатаОстатков,
			СтатусПартии <> ЗНАЧЕНИЕ(Перечисление.СтатусыПартийТоваров.НаКомиссию)
				И СтатусПартии <> ЗНАЧЕНИЕ(Перечисление.СтатусыПартийТоваров.ВозвратнаяТара)
				И Номенклатура.ВидНоменклатуры.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)) КАК ОстаткиТоваров

СГРУППИРОВАТЬ ПО
	ОстаткиТоваров.Номенклатура,
	ОстаткиТоваров.ХарактеристикаНоменклатуры,
	ОстаткиТоваров.Качество,
	ЕСТЬNULL(ОстаткиТоваров.ДокументОприходования.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)),
	ОстаткиТоваров.СерияНоменклатуры,
	ОстаткиТоваров.СерияНоменклатуры.НомерГТД,
	ОстаткиТоваров.СерияНоменклатуры.СтранаПроисхождения,
	ОстаткиТоваров.СерияНоменклатуры.ПометкаУдаления,
	ОстаткиТоваров.Номенклатура.ВестиУчетПоСериям,
	ОстаткиТоваров.ДокументОприходования

ИНДЕКСИРОВАТЬ ПО
	Организация
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ОстаткиТоваровПоПартиям.Организация КАК Организация,
	ОстаткиТоваровПоПартиям.Номенклатура КАК Номенклатура,
	ОстаткиТоваровПоПартиям.ДокументОприходования КАК ДокументОприходования,
	ОстаткиТоваровПоПартиям.ВестиУчетПоСериям КАК ВестиУчетПоСериям,
	ОстаткиТоваровПоПартиям.Характеристика КАК Характеристика,
	ОстаткиТоваровПоПартиям.Качество КАК Качество,
	ОстаткиТоваровПоПартиям.Серия КАК Серия,
	ОстаткиТоваровПоПартиям.НомерГТД КАК НомерГТД,
	ОстаткиТоваровПоПартиям.СтранаПроисхождения КАК СтранаПроисхождения,
	ОстаткиТоваровПоПартиям.ПометкаУдаления КАК ПометкаУдаления,
	ОстаткиТоваровПоПартиям.Количество,
	ОстаткиТоваровПоПартиям.СуммаИтогоУпр,
	ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчетСрезПоследних.СложныйУчетНДС, ЛОЖЬ) КАК СложныйУчетНДС,
	ЕСТЬNULL(УчетнаяПолитикаНалоговыйУчетСрезПоследних.ПартионныйУчетНДСвРазрезеСерийИХарактеристик, ЛОЖЬ) КАК УчетНДСВРазрезеСерийИХарактеристик
ПОМЕСТИТЬ ОстаткиПоПартиямСУчетнойПолитикой
ИЗ
	ОстаткиТоваровПоПартиям КАК ОстаткиТоваровПоПартиям
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&ДатаОстатков, ) КАК УчетнаяПолитикаНалоговыйУчетСрезПоследних
		ПО ОстаткиТоваровПоПартиям.Организация = УчетнаяПолитикаНалоговыйУчетСрезПоследних.Организация

ИНДЕКСИРОВАТЬ ПО
	Номенклатура,
	Организация,
	ДокументОприходования,
	УчетНДСВРазрезеСерийИХарактеристик
;

////////////////////////////////////////////////////////////////////////////////
ВЫБРАТЬ
	ОстаткиПоПартиямСУчетнойПолитикой.Организация КАК Организация,
	ОстаткиПоПартиямСУчетнойПолитикой.Номенклатура КАК Номенклатура,
	ОстаткиПоПартиямСУчетнойПолитикой.Характеристика,
	ОстаткиПоПартиямСУчетнойПолитикой.Качество,
	ОстаткиПоПартиямСУчетнойПолитикой.Серия,
	СУММА(ОстаткиПоПартиямСУчетнойПолитикой.Количество) КАК Количество,
	СУММА(ОстаткиПоПартиямСУчетнойПолитикой.СуммаИтогоУпр) КАК Сумма,
	МАКСИМУМ(ЕСТЬNULL(НДСПоПартиям.СтавкаНДС, ОстаткиПоПартиямСУчетнойПолитикой.Номенклатура.СтавкаНДС)) КАК СтавкаНДС,
	СУММА(ЕСТЬNULL(НДСПоПартиям.НДСОстаток, 0)) КАК НДСРегл,
	СУММА(ЕСТЬNULL(НДСПоПартиям.НДСОстаток, 0) * ВЫБОР
			КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) > 0
					И ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) > 0
				ТОГДА КурсыВалютСрезПоследних.Кратность / КурсыВалютСрезПоследних.Курс
			ИНАЧЕ 0
		КОНЕЦ) КАК СуммаНДС,
	СУММА(ОстаткиПоПартиямСУчетнойПолитикой.СуммаИтогоУпр * ВЫБОР
			КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) > 0
					И ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) > 0
				ТОГДА КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность
			ИНАЧЕ 0
		КОНЕЦ) КАК СуммаСНДСРегл,
	МАКСИМУМ(ОстаткиПоПартиямСУчетнойПолитикой.НомерГТД) КАК НомерГТД,
	МАКСИМУМ(ОстаткиПоПартиямСУчетнойПолитикой.СтранаПроисхождения) КАК СтранаПроисхождения
ИЗ
	ОстаткиПоПартиямСУчетнойПолитикой КАК ОстаткиПоПартиямСУчетнойПолитикой
		ЛЕВОЕ СОЕДИНЕНИЕ НДСПоПартиям КАК НДСПоПартиям
		ПО ОстаткиПоПартиямСУчетнойПолитикой.Номенклатура = НДСПоПартиям.Номенклатура
			И ОстаткиПоПартиямСУчетнойПолитикой.Организация = НДСПоПартиям.Организация
			И ОстаткиПоПартиямСУчетнойПолитикой.ДокументОприходования = НДСПоПартиям.СчетФактура
			И (НЕ ОстаткиПоПартиямСУчетнойПолитикой.УчетНДСВРазрезеСерийИХарактеристик
				ИЛИ ОстаткиПоПартиямСУчетнойПолитикой.Серия = НДСПоПартиям.СерияНоменклатуры)
			И (НЕ ОстаткиПоПартиямСУчетнойПолитикой.УчетНДСВРазрезеСерийИХарактеристик
				ИЛИ ОстаткиПоПартиямСУчетнойПолитикой.Характеристика = НДСПоПартиям.ХарактеристикаНоменклатуры)
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОстатков, Валюта = &ВалютаУпрУчета) КАК КурсыВалютСрезПоследних
		ПО (ИСТИНА)

СГРУППИРОВАТЬ ПО
	ОстаткиПоПартиямСУчетнойПолитикой.Организация,
	ОстаткиПоПартиямСУчетнойПолитикой.Номенклатура,
	ОстаткиПоПартиямСУчетнойПолитикой.Характеристика,
	ОстаткиПоПартиямСУчетнойПолитикой.Качество,
	ОстаткиПоПартиямСУчетнойПолитикой.Серия

УПОРЯДОЧИТЬ ПО
	Организация,
	Номенклатура,
	ОстаткиПоПартиямСУчетнойПолитикой.Характеристика,
	Серия,
	Качество

ИТОГИ ПО
	Организация
;

ВЫБРАТЬ
	ТоварыНаСкладах.Регистратор.Организация КАК Организация,
	ТоварыНаСкладах.Склад КАК Склад,
	ТоварыНаСкладах.Номенклатура КАК Номенклатура,
	ТоварыНаСкладах.Номенклатура.ВестиУчетПоСериям КАК ВестиУчетПоСериям,
	ТоварыНаСкладах.Номенклатура.ВестиПартионныйУчетПоСериям КАК ВестиПартионныйУчетПоСериям,
	ТоварыНаСкладах.ХарактеристикаНоменклатуры КАК Характеристика,
	ТоварыНаСкладах.СерияНоменклатуры КАК Серия,
	ТоварыНаСкладах.Качество КАК Качество,
	СУММА(ВЫБОР
			КОГДА ТоварыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
				ТОГДА ТоварыНаСкладах.Количество * ЕСТЬNULL(ТоварыНаСкладах.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
			ИНАЧЕ -ТоварыНаСкладах.Количество * ЕСТЬNULL(ТоварыНаСкладах.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
		КОНЕЦ) КАК Количество
ИЗ
	РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах

СГРУППИРОВАТЬ ПО
	ТоварыНаСкладах.Регистратор.Организация,
	ТоварыНаСкладах.Склад,
	ТоварыНаСкладах.Качество,
	ТоварыНаСкладах.ХарактеристикаНоменклатуры,
	ТоварыНаСкладах.Номенклатура,
	ТоварыНаСкладах.Номенклатура.ВестиУчетПоСериям,
	ТоварыНаСкладах.Номенклатура.ВестиПартионныйУчетПоСериям,
	ТоварыНаСкладах.СерияНоменклатуры

ИМЕЮЩИЕ
	СУММА(ВЫБОР
			КОГДА ТоварыНаСкладах.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
				ТОГДА ТоварыНаСкладах.Количество * ЕСТЬNULL(ТоварыНаСкладах.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
			ИНАЧЕ -ТоварыНаСкладах.Количество * ЕСТЬNULL(ТоварыНаСкладах.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
		КОНЕЦ) <> 0

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ТоварыВРознице.Регистратор.Организация,
	ТоварыВРознице.Склад,
	ТоварыВРознице.Номенклатура,
	ТоварыВРознице.Номенклатура.ВестиУчетПоСериям,
	ТоварыВРознице.Номенклатура.ВестиПартионныйУчетПоСериям,
	ТоварыВРознице.ХарактеристикаНоменклатуры,
	ТоварыВРознице.СерияНоменклатуры,
	ТоварыВРознице.Качество,
	СУММА(ВЫБОР
			КОГДА ТоварыВРознице.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
				ТОГДА ТоварыВРознице.Количество * ЕСТЬNULL(ТоварыВРознице.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
			ИНАЧЕ -ТоварыВРознице.Количество * ЕСТЬNULL(ТоварыВРознице.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
		КОНЕЦ)
ИЗ
	РегистрНакопления.ТоварыВРознице КАК ТоварыВРознице

СГРУППИРОВАТЬ ПО
	ТоварыВРознице.Склад,
	ТоварыВРознице.Номенклатура,
	ТоварыВРознице.ХарактеристикаНоменклатуры,
	ТоварыВРознице.СерияНоменклатуры,
	ТоварыВРознице.Качество,
	ТоварыВРознице.Регистратор.Организация,
	ТоварыВРознице.Номенклатура.ВестиУчетПоСериям,
	ТоварыВРознице.Номенклатура.ВестиПартионныйУчетПоСериям

ОБЪЕДИНИТЬ ВСЕ

ВЫБРАТЬ
	ТоварыВНТТ.Регистратор.Организация,
	ТоварыВНТТ.Склад,
	ТоварыВНТТ.Номенклатура,
	ТоварыВНТТ.Номенклатура.ВестиУчетПоСериям,
	ТоварыВНТТ.Номенклатура.ВестиПартионныйУчетПоСериям,
	ТоварыВНТТ.ХарактеристикаНоменклатуры,
	ТоварыВНТТ.СерияНоменклатуры,
	ЗНАЧЕНИЕ(Справочник.Качество.Новый),
	СУММА(ВЫБОР
			КОГДА ТоварыВНТТ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
				ТОГДА ТоварыВНТТ.Количество * ЕСТЬNULL(ТоварыВНТТ.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
			ИНАЧЕ -ТоварыВНТТ.Количество * ЕСТЬNULL(ТоварыВНТТ.Номенклатура.ЕдиницаХраненияОстатков.Коэффициент, 1)
		КОНЕЦ)
ИЗ
	РегистрНакопления.ТоварыВНТТ КАК ТоварыВНТТ

СГРУППИРОВАТЬ ПО
	ТоварыВНТТ.Склад,
	ТоварыВНТТ.Номенклатура,
	ТоварыВНТТ.ХарактеристикаНоменклатуры,
	ТоварыВНТТ.СерияНоменклатуры,
	ТоварыВНТТ.Регистратор.Организация,
	ТоварыВНТТ.Номенклатура.ВестиУчетПоСериям,
	ТоварыВНТТ.Номенклатура.ВестиПартионныйУчетПоСериям
	
УПОРЯДОЧИТЬ ПО
	Организация,
	Склад,
	Номенклатура,
	Качество
