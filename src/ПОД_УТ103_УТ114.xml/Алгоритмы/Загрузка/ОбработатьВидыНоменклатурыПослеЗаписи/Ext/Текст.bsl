// Определим склады, по которым есть документы ввода остатков с сериями
Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВводОстатковСерии.Ссылка.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ВводОстатковСерии.Номенклатура ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьОстатки,
	|	ВводОстатковСерии.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры
	|ПОМЕСТИТЬ ТаблицаОстатков
	|ИЗ
	|	Документ.ВводОстатков.Серии КАК ВводОстатковСерии
	|ГДЕ
	|	НЕ ВводОстатковСерии.Ссылка.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВводОстатковТовары.Ссылка.Склад,
	|	ВЫБОР
	|		КОГДА ВводОстатковТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	ВводОстатковТовары.Номенклатура.ВидНоменклатуры
	|ИЗ
	|	Документ.ВводОстатков.Товары КАК ВводОстатковТовары
	|ГДЕ
	|	НЕ ВводОстатковТовары.Ссылка.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыНоменклатуры.Ссылка КАК ВидНоменклатуры,
	|	ТаблицаОстатков.Склад,
	|	МАКСИМУМ(ЕСТЬNULL(ТаблицаОстатков.ЕстьОстатки, ЛОЖЬ)) КАК ЕстьОстатки
	|ИЗ
	|	Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатков КАК ТаблицаОстатков
	|		ПО ВидыНоменклатуры.Ссылка = ТаблицаОстатков.ВидНоменклатуры
	|ГДЕ
	|	ВидыНоменклатуры.ИспользоватьСерии
	|	И ЕстьОстатки 
	|
	|СГРУППИРОВАТЬ ПО
	|	ВидыНоменклатуры.Ссылка,
	|	ТаблицаОстатков.Склад
	|ИТОГИ ПО
	|	ВидНоменклатуры");
	
ВыборкаВидовНоменклатуры = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
Пока ВыборкаВидовНоменклатуры.Следующий() Цикл
	
	Объект = ВыборкаВидовНоменклатуры.ВидНоменклатуры.ПолучитьОбъект();
	
	// Заполнение настроек использования серий
	ТекущаяПолитикаУчетаСерий = Неопределено;
	Если Объект.ПолитикиУчетаСерий.Количество() > 0 Тогда
		ТекущаяПолитикаУчетаСерий = Объект.ПолитикиУчетаСерий[0].ПолитикаУчетаСерий;
	КонецЕсли;
	
	Объект.ПолитикиУчетаСерий.Очистить();
	
	Если ЗначениеЗаполнено(ТекущаяПолитикаУчетаСерий) И ВыборкаВидовНоменклатуры.ЕстьОстатки Тогда
		
		ДетальнаяВыборка = ВыборкаВидовНоменклатуры.Выбрать();
		Пока ДетальнаяВыборка.Следующий() Цикл
			НоваяСтрока                    = Объект.ПолитикиУчетаСерий.Добавить();
			НоваяСтрока.ПолитикаУчетаСерий = ТекущаяПолитикаУчетаСерий;
			НоваяСтрока.Склад              = ДетальнаяВыборка.Склад;
		КонецЦикла;
	КонецЕсли;
	
	// Запись не в режиме загрузки приведет к созданию наборов дополнительных реквизитов и установке признака использования серий у складов
	Объект.Записать();
	
КонецЦикла;
