Если Не ЗначениеЗаполнено(ВходящиеДанные) Тогда
	ВходящиеДанные = Новый Структура;
КонецЕсли;

ВходящиеДанные.Вставить("ТипНоменклатуры");

Если Параметры.ТабНоменклатурыТары.Найти(Источник.Ссылка, "Номенклатура") = Неопределено Тогда
	ВходящиеДанные.ТипНоменклатуры = ОбщегоНазначения.ПолучитьЗначениеРеквизита(Источник.ВидНоменклатуры, "ТипНоменклатуры");
Иначе
	ВходящиеДанные.ТипНоменклатуры = "МногооборотнаяТара";
КонецЕсли;

ПараметрыПоиска = Новый Структура("Номенклатура", Источник);
НайденныеСтроки = Параметры.ТабАлкогольнаяПродукцияНоменклатуры.НайтиСтроки(ПараметрыПоиска);

Если НайденныеСтроки.Количество() > 0 Тогда
	ВходящиеДанные.Вставить("АлкогольнаяПродукция",    НайденныеСтроки[0].АлкогольнаяПродукция);
	ВходящиеДанные.Вставить("ВидАлкогольнойПродукции", НайденныеСтроки[0].ВидАлкогольнойПродукции);
	ВходящиеДанные.Вставить("ВестиУчетПоГТД",          НайденныеСтроки[0].ВестиУчетПоГТД);
Иначе
	// ВидАлкогольнойПродукции и АлкогольнаяПродукция
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	СведенияОбАлкогольнойПродукции.ВидПродукции,
		|	СведенияОбАлкогольнойПродукции.ВидЛицензии
		|ИЗ
		|	РегистрСведений.СведенияОбАлкогольнойПродукции КАК СведенияОбАлкогольнойПродукции
		|ГДЕ
		|	СведенияОбАлкогольнойПродукции.Номенклатура = &Номенклатура";
		
	Запрос.УстановитьПараметр("Номенклатура", Источник.Ссылка);

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВходящиеДанные.Вставить("АлкогольнаяПродукция", Истина);
		ВходящиеДанные.Вставить("ВидАлкогольнойПродукции", Выборка.ВидПродукции);
	Иначе
		ВходящиеДанные.Вставить("АлкогольнаяПродукция", Ложь);
		ВходящиеДанные.Вставить("ВидАлкогольнойПродукции", Справочники.ВидыАлкогольнойПродукции.ПустаяСсылка());
	КонецЕсли;

	// Вести учет по ГТД
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1
	|ИЗ
	|	Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|ГДЕ
	|	СерииНоменклатуры.НомерГТД <> ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
	|	И СерииНоменклатуры.Владелец = &Номенклатура
	|");

	Запрос.УстановитьПараметр("Номенклатура", Источник.Ссылка);

	ВходящиеДанные.Вставить("ВестиУчетПоГТД", Не Запрос.Выполнить().Пустой());
	
	НоваяСтрокаАлко = Параметры.ТабАлкогольнаяПродукцияНоменклатуры.Добавить();
	НоваяСтрокаАлко.Номенклатура            = Источник;
	НоваяСтрокаАлко.АлкогольнаяПродукция    = ВходящиеДанные.АлкогольнаяПродукция;
	НоваяСтрокаАлко.ВестиУчетПоГТД          = ВходящиеДанные.ВестиУчетПоГТД;
	НоваяСтрокаАлко.ВидАлкогольнойПродукции = ВходящиеДанные.ВидАлкогольнойПродукции;

КонецЕсли;
