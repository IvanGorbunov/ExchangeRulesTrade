// Попытка получения результата из кэша предыдущих вычислений

СоздатьВидНоменклатуры_РезультатПоиска = Параметры.КешВидовНоменклатуры.Получить(СоздатьВидНоменклатуры_Номенклатура);
Если СоздатьВидНоменклатуры_РезультатПоиска <> Неопределено Тогда
	Значение = СоздатьВидНоменклатуры_РезультатПоиска;
КонецЕсли;

Если Не ЗначениеЗаполнено(Значение) Тогда

	// Алгоритм формирования вида номенклатуры
	// Запрос выбирает все свойства, назначенные для данной номенклатуры, кроме общих свойств номенклатуры
	ЗапросПоСвойствамНоменклатуры = Новый Запрос(
		"ВЫБРАТЬ
		|	НазначенияСвойствОбъектов.Объект КАК Назначение,
		|	НазначенияСвойствОбъектов.Объект.Наименование КАК НазначениеНаименование,
		|	СвойстваОбъектов.Ссылка КАК Свойство
		|ИЗ
		|	ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НазначенияСвойствОбъектов КАК НазначенияСвойствОбъектов
		|		ПО (НазначенияСвойствОбъектов.Свойство = СвойстваОбъектов.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО (
		|		(
		|		Номенклатура.Родитель = НазначенияСвойствОбъектов.Объект
		|		ИЛИ Номенклатура.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
		|		ИЛИ Номенклатура.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
		|		ИЛИ Номенклатура.Родитель.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
		|		ИЛИ Номенклатура.Родитель.Родитель.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
		|		)
		|		И Номенклатура.Ссылка = &Номенклатура
		|		)
		|ГДЕ
		|	СвойстваОбъектов.НазначениеСвойства = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Номенклатура)
		|	И НазначенияСвойствОбъектов.Объект.ЭтоГруппа
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НазначенияСвойствОбъектов.Объект,
		|	НазначенияСвойствОбъектов.Объект.Наименование КАК НазначениеНаименование,
		|	СвойстваОбъектов.Ссылка
		|ИЗ
		|	ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НазначенияСвойствОбъектов КАК НазначенияСвойствОбъектов
		|		ПО (НазначенияСвойствОбъектов.Свойство = СвойстваОбъектов.Ссылка)
		|ГДЕ
		|	СвойстваОбъектов.НазначениеСвойства = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_Номенклатура)
		|	И Не НазначенияСвойствОбъектов.Объект.ЭтоГруппа
		|	И НазначенияСвойствОбъектов.Объект = &Номенклатура");

	ЗапросПоСвойствамНоменклатуры.Параметры.Вставить("Номенклатура", СоздатьВидНоменклатуры_Номенклатура);

	// Запрос выбирает все свойства, назначенные для данной характеристики номенклатуры, кроме общих свойств характеристик
	ЗапросПоСвойствамХарактеристик = Новый Запрос(
		"ВЫБРАТЬ
		|	НазначенияСвойствОбъектов.Объект КАК Назначение,
		|	НазначенияСвойствОбъектов.Объект.Наименование КАК НазначениеНаименование,
		|	Номенклатура.Ссылка КАК Номенклатура,
		|	СвойстваОбъектов.Ссылка КАК Свойство
		|ИЗ
		|	ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НазначенияСвойствОбъектов КАК НазначенияСвойствОбъектов
		|		ПО (НазначенияСвойствОбъектов.Свойство = СвойстваОбъектов.Ссылка)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
		|		ПО (
		|		(
		|		Номенклатура.Родитель = НазначенияСвойствОбъектов.Объект
		|		ИЛИ Номенклатура.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
		|		ИЛИ Номенклатура.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
		|		ИЛИ Номенклатура.Родитель.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
		|		ИЛИ Номенклатура.Родитель.Родитель.Родитель.Родитель.Родитель = НазначенияСвойствОбъектов.Объект
		|		)
		|		И Номенклатура.Ссылка = &Номенклатура
		|		)
		|ГДЕ
		|	СвойстваОбъектов.НазначениеСвойства = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры)
		|	И Номенклатура.ВестиУчетПоХарактеристикам
		|	И НазначенияСвойствОбъектов.Объект.ЭтоГруппа
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НазначенияСвойствОбъектов.Объект,
		|	НазначенияСвойствОбъектов.Объект.Наименование КАК НазначениеНаименование,
		|	НазначенияСвойствОбъектов.Объект,
		|	СвойстваОбъектов.Ссылка
		|ИЗ
		|	ПланВидовХарактеристик.СвойстваОбъектов КАК СвойстваОбъектов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НазначенияСвойствОбъектов КАК НазначенияСвойствОбъектов
		|		ПО (НазначенияСвойствОбъектов.Свойство = СвойстваОбъектов.Ссылка)
		|ГДЕ
		|	СвойстваОбъектов.НазначениеСвойства = ЗНАЧЕНИЕ(ПланВидовХарактеристик.НазначенияСвойствКатегорийОбъектов.Справочник_ХарактеристикиНоменклатуры)
		|	И Не НазначенияСвойствОбъектов.Объект.ЭтоГруппа
		|	И НазначенияСвойствОбъектов.Объект.ВестиУчетПоХарактеристикам
		|	И НазначенияСвойствОбъектов.Объект = &Номенклатура");

	ЗапросПоСвойствамХарактеристик.Параметры.Вставить("Номенклатура", СоздатьВидНоменклатуры_Номенклатура);

	РезультатЗапросаПоСвойствамХарактеристик = ЗапросПоСвойствамХарактеристик.Выполнить();
	РезультатЗапросаПоСвойствамНоменклатуры  = ЗапросПоСвойствамНоменклатуры.Выполнить();

	Значение = Новый Структура;
	Значение.Вставить("ПометкаУдаления",   Ложь);
	Значение.Вставить("ТипНоменклатуры",   СоздатьВидНоменклатуры_ТипНоменклатуры);
	Значение.Вставить("ИспользоватьСерии", СоздатьВидНоменклатуры_ИспользоватьСерии);
	Значение.Вставить("АлкогольнаяПродукция",          СоздатьВидНоменклатуры_АлкогольнаяПродукция);
	Значение.Вставить("ИмпортнаяАлкогольнаяПродукция", СоздатьВидНоменклатуры_ИмпортнаяАлкогольнаяПродукция);
	Значение.Вставить("ВидАлкогольнойПродукции",       СоздатьВидНоменклатуры_ВидАлкогольнойПродукции);
	
	Если СоздатьВидНоменклатуры_Номенклатура.ВестиУчетПоХарактеристикам Тогда
		Значение.Вставить("ИспользованиеХарактеристик", ?(Параметры.СворачиватьХарактеристики, "ОбщиеДляВидаНоменклатуры", "ИндивидуальныеДляНоменклатуры"));
	Иначе
		Значение.Вставить("ИспользованиеХарактеристик", "НеИспользовать");
	КонецЕсли;

	Если СоздатьВидНоменклатуры_АлкогольнаяПродукция Тогда
		Если Значение.ИспользованиеХарактеристик = "НеИспользовать" Тогда
			Постфикс = "(б/х, алко)";
		ИначеЕсли Значение.ИспользованиеХарактеристик = "ОбщиеДляВидаНоменклатуры" Тогда
			Постфикс = "(о/х, алко)";
		Иначе
			Постфикс = "(и/х, алко)";
		КонецЕсли;
	Иначе
		Если Значение.ИспользованиеХарактеристик = "НеИспользовать" Тогда
			Постфикс = "(б/х)";
		ИначеЕсли Значение.ИспользованиеХарактеристик = "ОбщиеДляВидаНоменклатуры" Тогда
			Постфикс = "(о/х)";
		Иначе
			Постфикс = "(и/х)";
		КонецЕсли;
	КонецЕсли;	

	СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры = Лев(СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры, 49-СтрДлина(Постфикс)) + " " + Постфикс;

	Значение.Вставить("НаборСвойств",              Неопределено);
	Значение.Вставить("НаборСвойствХарактеристик", Неопределено);

	СоответствиеНаименование = Новый Соответствие;
	ЕстьНаборСвойств = Ложь;
	ЕстьНаборСвойствХарактеристик = Ложь;

	// Создаем набор свойств для характеристик
	Если Не РезультатЗапросаПоСвойствамХарактеристик.Пустой() Тогда
		
		ЕстьНаборСвойствХарактеристик = Истина;
		
		НаборСвойствХарактеристик = Новый Структура;
		Значение.Вставить("НаборСвойствХарактеристик", НаборСвойствХарактеристик);
		НаборСвойствХарактеристик.Вставить("ПометкаУдаления", Ложь);
		НаборСвойствХарактеристик.Вставить("Родитель", "Справочник_ХарактеристикиНоменклатуры");
		НаборСвойствХарактеристик.Вставить("ДополнительныеРеквизиты", Новый ТаблицаЗначений);
		
		НаборСвойствХарактеристик.ДополнительныеРеквизиты.Колонки.Добавить("Свойство");
		
		Выборка = РезультатЗапросаПоСвойствамХарактеристик.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			// Формирование строкового хэш из настройки назначения свойства
			Если ЗначениеЗаполнено(Выборка.НазначениеНаименование) Тогда
				СоответствиеНаименование.Вставить(Выборка.НазначениеНаименование);
			КонецЕсли;
			
			НовыеСвойство = НаборСвойствХарактеристик.ДополнительныеРеквизиты.Добавить();
			НовыеСвойство.Свойство = Выборка.Свойство;
			
		КонецЦикла;
		
	КонецЕсли;

	// Создаем набор свойств для номенклатуры
	Если Не РезультатЗапросаПоСвойствамНоменклатуры.Пустой() Тогда
		
		ЕстьНаборСвойств = Истина;
		
		НаборСвойств = Новый Структура;
		Значение.Вставить("НаборСвойств", НаборСвойств);
		НаборСвойств.Вставить("ПометкаУдаления", Ложь);
		НаборСвойств.Вставить("Родитель", "Справочник_Номенклатура");
		НаборСвойств.Вставить("ДополнительныеРеквизиты", Новый ТаблицаЗначений);
		
		НаборСвойств.ДополнительныеРеквизиты.Колонки.Добавить("Свойство");
		
		Выборка = РезультатЗапросаПоСвойствамНоменклатуры.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			// Формирование строкового хэш из настройки назначения свойства
			Если ЗначениеЗаполнено(Выборка.НазначениеНаименование) Тогда
				СоответствиеНаименование.Вставить(Выборка.НазначениеНаименование);
			КонецЕсли;
			
			НовыеСвойство = НаборСвойств.ДополнительныеРеквизиты.Добавить();
			НовыеСвойство.Свойство = Выборка.Свойство;
			
		КонецЦикла;
		
	КонецЕсли;

	// Формирование наименования
	ПерваяИтерация = Истина;
	Для Каждого КлючИЗначение Из СоответствиеНаименование Цикл
		Если ПерваяИтерация Тогда
			СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры = СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры + " (" + КлючИЗначение.Ключ;	
			ПерваяИтерация = Ложь;
		Иначе
			СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры = СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры + ", " + КлючИЗначение.Ключ;
		КонецЕсли;
	КонецЦикла;

	Если Не ПерваяИтерация Тогда
		СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры = СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры + ")";
	КонецЕсли;
		
	Если ЕстьНаборСвойствХарактеристик Тогда
		СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры = СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры + " (Для характеристик)";
	КонецЕсли;

	// Длина наименования не должна превышать 50 символов.
	СоздатьВидНоменклатуры_Наименование = Лев(СокрЛП(СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры), 50);

	Значение.Вставить("Наименование", СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры);
	
	Если ЕстьНаборСвойств Тогда
		НаборСвойств.Вставить("Наименование", СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры);
	КонецЕсли;

	Если ЕстьНаборСвойствХарактеристик Тогда
		НаборСвойствХарактеристик.Вставить("Наименование", СоздатьВидНоменклатуры_НаименованиеВидаНоменклатуры + "(Для характеристик)");
	КонецЕсли;

	Если Значение.ТипНоменклатуры = "МногооборотнаяТара" Тогда
		Значение.Наименование = Значение.Наименование + " (Тара)";
	КонецЕсли;

	Параметры.КешВидовНоменклатуры.Вставить(СоздатьВидНоменклатуры_Номенклатура, Значение);
	
КонецЕсли;
